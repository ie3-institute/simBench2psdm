plugins {
	id "com.jfrog.artifactory" version "5.1.11"
	id 'groovy' // groovy support
	id 'java' // java support
	id 'scala' // scala support
	id 'pmd' // code check, working on source code
	id 'com.diffplug.spotless' version '7.2.1'// code format
	id "de.undercouch.download" version "5.6.0" // downloads plugin
	id 'jacoco' // java code coverage plugin
	id "org.sonarqube" version "6.2.0.5505" // sonarqube
	id 'org.scoverage' version '8.1' // Code coverage plugin for scala
	id 'net.thauvin.erik.gradle.semver' version '1.0.4' // semantic versioning
	id "com.github.maiflai.scalatest" version "0.33" // run scalatest without specific task
}

ext {
	//version (changing these should be considered thoroughly!)
	javaVersion = JavaVersion.VERSION_17

	scalaVersion = '2.13'
	scalaBinaryVersion = '2.13.16'
	tscfgVersion = '1.2.5'
	slf4jVersion = '2.0.17'

	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script plugins
}

group = 'com.github.ie3-institute'
description = 'simbench2psdm'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'tscfg.gradle' // config tasks
apply from: scriptsLocation + 'scoverage.gradle'
apply from: scriptsLocation + 'semVer.gradle'
apply from: scriptsLocation + 'branchName.gradle'
apply from: scriptsLocation + 'mavenCentralPublish.gradle'

repositories {
	mavenCentral()
	maven { url 'https://www.jitpack.io' } // allows github repos as dependencies

	// sonatype snapshots repo // todo remove
	maven { url 'https://oss.sonatype.org/service/local/repositories/snapshots/content' }
}

dependencies {
	/* PowerSystemDataModel */
	implementation('com.github.ie3-institute:PowerSystemDataModel:8.1.0') {
		exclude group: 'org.apache.logging.log4j'
		exclude group: 'org.slf4j'
		/* Exclude our own nested dependencies */
		exclude group: 'com.github.ie3-institute'
	}

	/* util functions */
	implementation('com.github.ie3-institute:PowerSystemUtils:2.2.1') {
		exclude group: 'org.apache.logging.log4j'
		exclude group: 'org.slf4j'
		/* Exclude our own nested dependencies */
		exclude group: 'com.github.ie3-institute'
	}

	implementation 'org.codehaus.groovy:groovy:3.0.25'

	implementation 'tech.units:indriya:2.2.3'
	implementation 'org.locationtech.jts:jts-core:1.20.0'
	implementation 'commons-io:commons-io:2.20.0'

	// logging
	implementation 'org.apache.logging.log4j:log4j-api:+' // log4j
	implementation 'org.apache.logging.log4j:log4j-core:+' // log4j
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:+' // log4j -> slf4j

	implementation "com.typesafe.scala-logging:scala-logging_${scalaVersion}:+" // akka scala logging
	implementation "org.slf4j:log4j-over-slf4j:${slf4jVersion}" // slf4j -> log4j

	// NEW scala libs //
	// CORE Scala //
	implementation "org.scala-lang:scala-library:$scalaBinaryVersion"
	implementation 'org.scala-lang.modules:scala-parallel-collections_2.13:1.2.0'

	// TEST Scala //
	testImplementation "org.scalatest:scalatest_${scalaVersion}:3.2.19"
	testImplementation 'com.vladsch.flexmark:flexmark-all:0.64.8'
	testImplementation "org.pegdown:pegdown:1.6.0" // HTML report for scalatest
	implementation 'org.mockito:mockito-core:5.19.0' // mocking framework

	// config //
	implementation 'com.typesafe:config:+'
	implementation "com.github.carueda:tscfg_2.13:${tscfgVersion}"

	// cmd args parser //
	implementation "com.github.scopt:scopt_${scalaVersion}:+"

	/* Handling compressed archives */
	implementation "org.apache.commons:commons-compress:+"
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

tasks.named('test') {
	dependsOn tasks.named('compileScoverageGroovy')
	dependsOn tasks.named('processScoverageResources')
}
