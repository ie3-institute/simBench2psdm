plugins {
	id "com.jfrog.artifactory" version "4.13.0"
	id 'groovy' // groovy support
	id 'java' // java support
	id 'scala' // scala support
	id 'pmd' // code check, working on source code
	id 'com.diffplug.gradle.spotless' version '4.3.1'// code format
	id "de.undercouch.download" version "3.4.3" // downloads plugin
	id 'jacoco' // java code coverage plugin
	id "org.sonarqube" version "2.8" // sonarqube
	id 'org.scoverage' version '4.0.2' // Code coverage plugin for scala
	id "com.github.maiflai.scalatest" version "0.26" // run scalatest without specific task
}

ext {
	//version (changing these should be considered thoroughly!)
	javaVersion = JavaVersion.VERSION_1_8

	scalaVersion = '2.13'
	tscfgVersion = '0.9.98'
	slf4jVersion = '1.7.26'

	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script plugins
}

group = 'com.github.ie3-institute'
description = 'simbench4ie3'
version = '1.0-SNAPSHOT'

apply from: scriptsLocation + 'pmd.gradle'
apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'checkJavaVersion.gradle'
apply from: scriptsLocation + 'tscfg.gradle' // config tasks
apply from: scriptsLocation + 'scoverage.gradle'

repositories {
	mavenCentral()
	maven { url 'https://www.jitpack.io' } // allows github repos as dependencies

	// sonatype snapshots repo // todo remove
	maven { url 'https://oss.sonatype.org/service/local/repositories/snapshots/content' }
}

dependencies {
	/* PowerSystemDataModel */
	implementation ('com.github.ie3-institute:PowerSystemDataModel:2.1-SNAPSHOT') {
		/* Exclude nested logging and ie3 related dependencies */
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'com.github.ie3-institute'
	}

	/* util functions */
	implementation ('com.github.ie3-institute:PowerSystemUtils:1.5.3') {
		/* Exclude nested logging and ie3 related dependencies */
		exclude group: 'org.slf4j', module: 'slf4j-api'
		exclude group: 'com.github.ie3-institute'
	}

	implementation 'org.codehaus.groovy:groovy:3.0.3'

	implementation 'tech.units:indriya:2.0.4'

	// logging
	implementation 'org.apache.logging.log4j:log4j-api:+' // log4j
	implementation 'org.apache.logging.log4j:log4j-core:+' // log4j
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:+' // log4j -> slf4j

	implementation "com.typesafe.scala-logging:scala-logging_${scalaVersion}:+" // akka scala logging
	implementation "org.slf4j:log4j-over-slf4j:${slf4jVersion}" // slf4j -> log4j

	// NEW scala libs //
	// CORE Scala //
	implementation "org.scala-lang:scala-library:${scalaVersion}+"

	// TEST Scala //
	testImplementation "org.scalatest:scalatest_${scalaVersion}:3.2.0"
	testImplementation 'com.vladsch.flexmark:flexmark:0.35.6' // FIXME: Obviously some missing dependency
	testImplementation 'com.vladsch.flexmark:flexmark-profile-pegdown:0.35.6' // FIXME: Obviously some missing dependency
	testImplementation "org.pegdown:pegdown:1.6.0" // HTML report for scalatest
	implementation 'org.mockito:mockito-core:3.4.4' // mocking framework
	scoverage "org.scoverage:scalac-scoverage-plugin_${scalaVersion}:1.4.1"
	scoverage "org.scoverage:scalac-scoverage-runtime_${scalaVersion}:1.4.1"

	// config //
	implementation 'com.typesafe:config:+'
	implementation "com.github.carueda:tscfg_2.13:${tscfgVersion}"

	// cmd args parser //
	implementation "com.github.scopt:scopt_${scalaVersion}:+"

	/* Handling compressed archives */
	implementation "org.apache.commons:commons-compress:+"
}

wrapper {
	gradleVersion = '7.1.1'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}
