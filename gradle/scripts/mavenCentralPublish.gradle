/* Maven publish - start */
tasks.register("sourcesJar", Jar) {
	archiveClassifier.set("sources")
	from(sourceSets.main.allJava)
}

tasks.register("javadocJar", Jar) {
	dependsOn(tasks.named("javadoc"))
	archiveClassifier.set("javadoc")
	from(tasks.named("javadoc").get().destinationDir)
}

if (project.hasProperty('user') && project.hasProperty('password')) {
	signing {
		required { !version.endsWith('SNAPSHOT') }
		if(required)
			sign(publishing.publications)
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {

				versionMapping {
					// resolves dynamic versioning to current version number
					usage('java-api') {
						fromResolutionOf('runtimeClasspath')
					}
					usage('java-runtime') {
						fromResolutionResult()
					}
				}
				pom {
					description = 'Tool to convert SimBench data sets to ieÂ³\'s power system data model'
					name = 'simBench2psdm'
					url = 'https:github.com/ie3-institute/simBench2psdm'
					organization {
						name = 'Institute of Energy Systems, Energy Efficiency and Energy Economics (ie3)/TU Dortmund University'
						url = 'https:www.ie3.tu-dortmund.de'
					}
					issueManagement {
						system = 'GitHub'
						url = 'https:github.com/ie3-institute/simBench2psdm/issues'
					}
					licenses {
						license {
							name = 'BSD 3-Clause License'
							url = 'https:github.com/ie3-institute/simBench2psdm/blob/main/LICENSE'
						}
					}
					developers {
						developer {
							organization = "Institute of Energy Systems, Energy Efficiency and Energy Economics (ie3)/TU Dortmund University"
							organizationUrl = "https:ie3.etit.tu-dortmund.de"
						}
					}
					scm {
						connection = 'scm:git:git:github.com/ie3-institute/simBench2psdm.git'
						developerConnection = 'scm:git:ssh:github.com:ie3-institute/simBench2psdm.git'
						url = 'https:github.com/ie3-institute/simBench2psdm'
					}
				}

				removeTestDependenciesFromPom(pom)
				groupId group
				artifactId 'simBench2psdm'
				version version

				from components.java
				artifact sourcesJar
				artifact javadocJar
			}
		}
		repositories {
			maven {
				def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
				def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
				url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
				credentials {
					username project.getProperty('user')
					password project.getProperty('password')
				}
			}
		}
	}


	model {
		tasks.generatePomFileForMavenJavaPublication {
			destination = file("$rootDir/build/generated-pom.xml")
		}
	}
}

def removeTestDependenciesFromPom(pom) {

	pom.withXml {
		def root = asNode()
		// eliminate test-scoped dependencies (no need in maven central POMs)
		root.dependencies.removeAll { dep ->
			dep.scope == "test"
		}
	}
}


/* Maven publish - end */
